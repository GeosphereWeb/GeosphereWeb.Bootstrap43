##
# "Image" element
#
prototype(GeosphereWeb.Bootstrap43:Content.ResponsiveImage) < prototype(Neos.Neos:ContentComponent) {

    image = Neos.Fusion:Case {
        asset {
            condition = ${q(node).property('image')}
            renderer = Neos.Neos:ImageUri {
                asset = ${q(node).property('image')}
                width = ${q(node).property('width')}
                height = ${q(node).property('height')}
                allowCropping = TRUE
                allowUpScaling = TRUE
            }
        }
        dummy {
            condition = ${node.context.inBackend}
            renderer = Neos.Fusion:ResourceUri {
                path = 'resource://Neos.Neos/Public/Images/dummy-image.svg'
            }
        }
    }

    alt = ${q(node).property('alternativeText') ? q(node).property('alternativeText') : null}
    title = ${q(node).property('title') ? q(node).property('title') : null}
    class = Neos.Fusion:Join {
        styleoption = ${Array.join(q(node).property('styleOptions'), ' ')}
        styleoption.@if.isSelected = ${q(node).property('styleOptions')}
        align = ${q(node).property('aligning')}
        align.@if.isSelected = ${q(node).property('aligning')}
        additionalClass = 'werner'
        @glue = ' '
    }

    renderer = afx`
            <img src={props.image} title={props.title} alt={props.alt} class={props.class} />
    `
}