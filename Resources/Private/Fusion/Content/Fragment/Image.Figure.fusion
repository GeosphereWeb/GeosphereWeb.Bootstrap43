##
# Figure fragment for text and textWithImage
#
prototype(GeosphereWeb.Bootstrap43:Content.Fragment.Image.Figure) < prototype(Neos.Fusion:Component) {

    image = Neos.Fusion:Case {
        asset {
            condition = ${q(node).property('image')}
            renderer = Neos.Neos:ImageUri {
                asset = ${q(node).property('image')}
            }
        }
        dummy {
            condition = ${node.context.inBackend}
            renderer = Neos.Fusion:ResourceUri {
                path = 'resource://Neos.Neos/Public/Images/dummy-image.svg'
            }
        }
    }


    alt = ${q(node).property('alternativeText') ? q(node).property('alternativeText') : null}
    title = ${q(node).property('title') ? q(node).property('title') : null}
    class = null

    hasCaption = ${q(node).property('hasCaption')}
    caption = Neos.Neos:Editable {
        property = 'caption'
    }

    renderer = afx`
        <figure class={props.class}>
            <img src={props.image} title={props.title} alt={props.alt} />
            <figcaption @if.has={props.hasCaption && props.caption}>
                {props.caption}
            </figcaption>
        </figure>
    `
}

